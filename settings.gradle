pluginManagement {
	includeBuild "gradle/plugins/aot-smoke-test-plugin"
	repositories {
		maven {
			url "https://repo.spring.vmware.com/artifactory/spring-commercial-snapshot-local"
			credentials {
				username System.env['REPO_SPRING_VMWARE_COM_USERNAME']
				password System.env['REPO_SPRING_VMWARE_COM_PASSWORD']
			}
			mavenContent {
				snapshotsOnly()
				includeGroupByRegex("org.springframework.*")
			}
		}
		maven {
			url "https://repo.spring.vmware.com/artifactory/spring-commercial-release-local"
			credentials {
				username System.env['REPO_SPRING_VMWARE_COM_USERNAME']
				password System.env['REPO_SPRING_VMWARE_COM_PASSWORD']
			}
			mavenContent {
				releasesOnly()
				includeGroupByRegex("org.springframework.*")
			}
		}
		gradlePluginPortal()
		maven {
			url "https://repo.spring.io/snapshot"
			mavenContent {
				snapshotsOnly()
			}
		}
		maven {
			url "https://repo.spring.io/milestone"
			mavenContent {
				releasesOnly()
			}
		}
	}
}

plugins {
	id "com.gradle.develocity" version "3.17.3"
	id "io.spring.ge.conventions" version "0.0.17"
}

rootProject.name="spring-aot-smoke-tests"

include "aot-smoke-test-support"
include "aot-smoke-test-third-party-hints"

["batch", "boot", "cloud", "data", "framework", "graphql", "integration", "security", "session"].each { group ->
	file(group).eachDirMatch(~/[a-z].*/) { smokeTest ->
		include "$group:${smokeTest.name}"
	}
}

develocity {
	buildScan {
		value("Spring Boot generation", "${springBootGeneration}")
	}
}